" Vim filetype plugin file
"
"   Language:  MakeFile
"     Plugin:  make.vim
" Maintainer:  Sarrow
"       Date:  2008åä¸€æœˆ13
" LastModify:  2008åä¸€æœˆ25
"    Version:  0.11
"
" This will enable some completion utility for mingw.Makefile
"
" using Vim's dictionary feature |i_CTRL-X_CTRL-K|.
" 	      Omin-complete utility |i_CTRL-X_CTRL-O|.
" -----------------------------------------------------------------
"
" Only do this when not done yet for this buffer
"
if exists("b:did_make_ftplugin")
  finish
endif
let b:did_make_ftplugin = 1

" ---------- Usr Setting Variable List --------------------------------- " {{{ 1
let maplocalleader = ";"

" ---------- User-Key mappings List ------------------------------------ " {{{1

"if !exists("g:completekey") {{{2
"    "Sarrow:change the default key setting
"    let g:completekey = "<c-j>"   "hotkey
"endif

"execute "inoremap <buffer> <silent>	".g:completekey."	<C-r>=call <SID>Insert_keyword_mapping_and_jump_regin()<CR>"
"}}}2

inoremap <buffer> <silent>	<C-j>			<C-r>=<SID>Insert_keyword_mapping_and_jump_regin()<CR>

nnoremap <buffer> <silent>	<LocalLeader>ma		:call <SID>Call_Make("all")<CR>
nnoremap <buffer> <silent>	<LocalLeader>mr		:call <SID>Call_Make("release")<CR>
nnoremap <buffer> <silent>	<LocalLeader>md		:call <SID>Call_Make("debug")<CR>
nnoremap <buffer> <silent>	<LocalLeader>mc		:call <SID>Call_Make("clean")<CR>
nnoremap <buffer> <silent>	<LocalLeader>mp		:call <SID>Call_Make("depends")<CR>
nnoremap <buffer> <silent>	<LocalLeader>mg		:call <SID>Set_Make_Argument()<CR>

" ---------- Makefile User Specified 3rd Party Library dictionary List ------ " {{{1
" Variable Name: g:Mingw_usr_lib_file
" 	   Type: type("")
if !exists("g:Mingw_usr_lib_file")
    let g:Mingw_usr_lib_file = globpath(&runtimepath, 'ftplugin/make_res/mingw.lib.lst')
endif

let b:Makefile_argument = ''

function! <SID>Call_Make(ag)
    " update : write source file if necessary
    exe	":update"
    " run make

    " Sarrow: below 2 line are modified, at date 2008/10/8
    " original is: >
    " 	exe	":!make ".s:C_MakeCmdLineArgs
    " <
    silent exe	"make -f % ". a:ag . b:Makefile_argument
    exe	":botright cwindow"
endfunction

function! <SID>Set_Make_Argument()
    let b:Makefile_argument = input("Plz. input your makefile arguments: ")
endfunction

" ---------- User-Template mappings List ------------------------------- " {{{1
"
" æ£€æŸ¥å½“å‰bufferä¸­æœ‰æ–‡å­—æ²¡æœ‰ï¼›å¦‚æœæ˜¯ç©ºçš„ï¼Œå°±ä»å¤–éƒ¨æ–‡ä»¶ä¸­è¯»å…¥æ¨¡æ¿ï¼Œç„¶åå†™å…¥ï¼›å
" ä¹‹ï¼Œåˆ™è¯¢é—®ç”¨æˆ·ï¼Œè®©ç”¨æˆ·åœ¨â€˜æ”¾å¼ƒåŸæ–‡â€™ã€â€˜ç¼€åˆ°å°¾éƒ¨â€™ã€â€˜æ”¾å¼ƒâ€™ä¸‰ä¸ªé€‰é¡¹ä¸­é€‰æ‹©ä¸€
" ä¸ªã€‚
if !exists("g:Mingw_make_template")
    function! Load_mingw_make_template_entry()
	" æ¨¡æ¿ä»¥åŠå¯¹åº”çš„å¿«æ·é”®éƒ½æ˜¯ç”¨æˆ·è‡ªå®šä¹‰çš„ã€‚
	" å¿«æ·é”®å–æ–‡ä»¶åä¸­éƒ¨çš„è‡³å¤šå‰4ä¸ªå­—ç¬¦ã€‚
	" è¦æ·»åŠ æ–°çš„æ¨¡æ¿æ–‡ä»¶ï¼Œç›´æ¥æ–°å»ºmake.*.templateæ ·å¼çš„æ–‡ä»¶å³å¯ï¼›è¦ä¿®æ”¹å¿«
	" æ·é”®ï¼Œä¿®æ”¹å¯¹åº”çš„æ–‡ä»¶åå³å¯ã€‚
	let entry_list = split(globpath(&runtimepath, 'ftplugin/make_res/make.*.mak'), "\n")
	let ret_val = {}
	for entry in entry_list
	    " VIM:
	    " 	:h filename-modifiers
	    let entry_fname = matchstr(fnamemodify(entry,':t'), '^make\.\zs[^.]\+')
	    let entry_pat = entry_fname[0:3]
	    " ä»…middle nameçš„å‰4ä¸ªå­—ç¬¦ä½œä¸ºå¿«æ·é”®
	    "let entry_pat = entry_fname[0:3]
	    let ret_val[entry_pat] = {'path': entry}
	    execute 'nnoremap <buffer> <silent> <LocalLeader>'.entry_pat.' :call Load_make_template("'.escape(entry,'\ ').'")<CR>'
	    " ç»‘å®šèœå•
	    execute 'nmenu &Make.&Load\ Template.'.entry_fname.' :call Load_make_template("'.escape(entry,'\ ').'")<CR>'
	endfor
	return ret_val
    endfunction
    let g:Mingw_make_template = Load_mingw_make_template_entry()
endif

" :h Funcref function()
function! Load_make_template(template_path) " {{{2

    " State Value:
    " 1) Append at end
    " 2) Overwrite
    " 3) Cancel -- default
    let state = 3

    let save_ruler = &ruler
    let save_showcmd = &showcmd
    set noruler noshowcmd
    if line('$') == 1 && getline(1) == ''
	let state = 1
    else
	let state = confirm("Waring: Non-empty file!\nLoad template anyway?", "&Append\n&Overwrite\n&Cancel", 3, "W")
	if state == 3
	    return
	elseif state == 1
	    execute '$'
	elseif state == 2
	    " delete All to black-hole, then changes to "Append" state
	    execute "1,$delete _"
	    execute '$'
	    let state = 1
	endif
    endif
    let content = join(readfile(a:template_path), "\n")
    for key in keys(g:Mingw_Make_Template_Operator)
        let replace = g:Mingw_Make_Template_Operator[key]()
        let content = substitute(content, '\s*\$'.key.'\$', escape(replace, '\'), 'g')
    endfor
    setlocal paste
    normal $"=contentp
    "normal A=content
    setlocal nopaste
    let &ruler = save_ruler
    let &showcmd = save_showcmd
endfunction " }}}2

if !exists("g:Mingw_Make_Template_Operator") " {{{1
    let g:Mingw_Make_Template_Operator={}
    " éœ€è¦æ”¯æŒæ›´å¤šçš„æ›¿æ¢å…³é”®å­—ï¼Œå¯ä»¥ä»¿é€ ä¸‹é¢çš„æ ¼å¼ï¼Œè‡ªè¡Œæ·»åŠ å³å¯ã€‚
    function g:Mingw_Make_Template_Operator['C']() dict
	return s:List_Current_File(g:Make_Complete_extensions['C'])
    endfunction
    function g:Mingw_Make_Template_Operator['CPP']() dict
	return s:List_Current_File(g:Make_Complete_extensions['CPP'])
    endfunction
    function g:Mingw_Make_Template_Operator['RES']() dict
	return s:List_Current_File(g:Make_Complete_extensions['RES'])
    endfunction
    function g:Mingw_Make_Template_Operator['SRC']() dict
	return s:List_Current_File(g:Make_Complete_extensions['SRC'])
    endfunction
    function g:Mingw_Make_Template_Operator['H']() dict
	return s:List_Current_File(g:Make_Complete_extensions['H'])
    endfunction
    function g:Mingw_Make_Template_Operator['HPP']() dict
	return s:List_Current_File(g:Make_Complete_extensions['HPP'])
    endfunction
    function g:Mingw_Make_Template_Operator['HH']() dict
	return s:List_Current_File(g:Make_Complete_extensions['HH'])
    endfunction
    function g:Mingw_Make_Template_Operator['ALL']() dict
	return s:List_Current_File(g:Make_Complete_extensions['ALL'])
    endfunction
    function g:Mingw_Make_Template_Operator['DATE']() dict
	return strftime("%Y %b %d %X")
    endfunction
    function g:Mingw_Make_Template_Operator['DIRNAME']() dict
	return expand("%:p:h:t")
    endfunction
    function g:Mingw_Make_Template_Operator['FNAME_NO_EXT']() dict
	return expand("%:r")
    endfunction
    function g:Mingw_Make_Template_Operator['FNAME']() dict
	return expand("%")
    endfunction
    function g:Mingw_Make_Template_Operator['FNAME_EXT']() dict
	return expand("%:e")
    endfunction
    function g:Mingw_Make_Template_Operator['CWP']() dict
	return expand("%:p:h")
    endfunction
endif

if !exists("g:Make_Complete_extensions") " {{{1
    let g:Make_Complete_extensions = {
		\'C': 	['c'],
		\'CPP': ['cpp', 'cxx', 'cc'],
		\'RES': ['rc'],
		\'H': 	['h'],
		\'HPP': ['hpp']}
    let g:Make_Complete_extensions['SRC'] =
		\g:Make_Complete_extensions['C'] +
		\g:Make_Complete_extensions['CPP'] +
		\g:Make_Complete_extensions['RES']
    let g:Make_Complete_extensions['HH'] = g:Make_Complete_extensions['H'] + g:Make_Complete_extensions['HPP']
    let g:Make_Complete_extensions['ALL'] = g:Make_Complete_extensions['SRC'] + g:Make_Complete_extensions['HH']
endif

function! <SID>Insert_keyword_mapping_and_jump_regin() " {{{1
    let aword = s:Get_last_word()
    if !has_key(g:Mingw_Make_Template_Operator, aword)
	return s:SwitchRegion()
    endif
    let complete = g:Mingw_Make_Template_Operator[aword]()
    let complete = substitute(complete, '\t', '', 'g')
    if complete != ""
	return "normal \<c-w>".complete
	" åœ¨ä¸ç¦»å¼€æ’å…¥æ¨¡å¼çš„æƒ…å†µä¸‹ï¼Œåˆ é™¤å…‰æ ‡ä¹‹å‰çš„ä¸€ä¸ªå•è¯ï¼š
	" execute "normal i\<c-w>\<c-o>l"
    else
	echohl WarningMsg
	echomsg 'No file extension with ' . string(g:Make_Complete_extensions[aword]) . ' found!'
	echomsg 'Plz. check your Current Work Directory!'
	echohl None
	return ''
    endif
endfunction

" Sarrow:2008åä¸€æœˆ24
" this function comes from code_complete.vim
function! s:SwitchRegion()
    " if s:jumppos != -1
    "     call cursor(s:jumppos,0)
    "     let s:jumppos = -1
    " endif
    let rs = escape(g:Mingw_make_functions['R_S'], '~%^$\')
    let re = escape(g:Mingw_make_functions['R_E'], '~%^$\')
    if match(getline('.'),rs.'.*'.re)!=-1 || search(rs.'.\{-}'.re)!=0
        normal 0
        call search(rs,'c',line('.'))
        normal v
        call search(re,'e',line('.'))
        if &selection == "exclusive"
            exec "normal " . "\<right>"
        endif
        return "\<c-\>\<c-n>gvo\<c-g>"
    else
        "if s:doappend == 1
	"    if g:completekey == "<tab>"
        "        return "\<tab>"
        "    endif
        "endif
        return ''
    endif
endfunction

function! s:Get_last_word()
    return substitute(getline('.')[:(col('.')-1)], '\zs.*\W\ze\w*$', '', 'g')
endfunction

" æ ¹æ®æä¾›çš„åç¼€åˆ—è¡¨ï¼ŒæŠŠå½“å‰ç›®å½•çš„æ–‡ä»¶åˆ—å‡ºæ¥ã€‚å¹¶è‡ªåŠ¨æ·»åŠ ç»­è¡Œç¬¦ä»¥åŠescapeæ‰ç©ºæ ¼
function! s:List_Current_File(ext) " {{{1
    let file_list = []
    if type(a:ext) == type("")
	" æ³¨æ„ï¼Œglobpath()è¿”å›çš„æ˜¯ä¸€ä¸ªå­—ç¬¦ä¸²ï¼
	let file_list = split(globpath('.', '*.'.a:ext), "\n")
    elseif type(a:ext) == type([])
	for item in a:ext
	    call extend(file_list, split(globpath('.', '*.'.item), "\n"))
	endfor
    endif
    let file_list2 = []
    for item in file_list
	call extend(file_list2, [substitute(item, '^\.\', "\t", 'g')])
    endfor
    return join(file_list2, "\\\n")
endfunction

if !exists("g:Mingw_tool_para_dict") " {{{1
    let g:Mingw_tool_para_dict = {}
    "   g:Mingw_tool_para_dict['windres']=[...]{{{2
    let g:Mingw_tool_para_dict['windres']=[
                \{'word': '-i',  'menu': '--input=<file>'},
                \{'word': '-o',  'menu': '--output=<file>'},
                \{'word': '-J',  'menu': '--input-format=<format>'},
                \{'word': '-O',  'menu': '--output-format=<format>'},
                \{'word': '-F',  'menu': '--target=<target>'},
                \{'word': '-I',  'menu': '--include-dir=<dir>'},
                \{'word': '-D',  'menu': '--define <sym>[=<val>]'},
                \{'word': '-U',  'menu': '--undefine <sym>'},
                \{'word': '-v',  'menu': '--verbose'},
                \{'word': '-l',  'menu': '--language=<val>'},
                \{'word': '-r'},
                \{'word': '-h',  'menu': '--help'},
                \{'word': '-V',  'menu': '--version'}]

    "   g:Mingw_tool_para_dict['gcc']=[...] {{{2
    let g:Mingw_tool_para_dict['gcc']=[
                \'-m128bit-long-double',
                \'-m32',
                \'-m3dnow',
                \'-m64',
                \'-m80387',
                \'-m96bit-long-double',
                \'-maccumulate-outgoing-args',
                \'-malign-double',
                \'-malign-functions=',
                \'-malign-jumps=',
                \'-malign-loops=',
                \'-malign-stringops',
                \'-march=',
                \'-masm=',
                \'-mbranch-cost=',
                \'-mcmodel=',
                \'-mconsole',
                \'-mcygwin',
                \'-mdll',
                \'-mfancy-math-387',
                \'-mfp-ret-in-387',
                \'-mfpmath=',
                \'-mhard-float',
                \'-mieee-fp',
                \'-minline-all-stringops',
                \'-mmmx',
                \'-mms-bitfields',
                \'-mno-3dnow',
                \'-mno-80387',
                \'-mno-accumulate-outgoing-args',
                \'-mno-align-double',
                \'-mno-align-stringops',
                \'-mno-cygwin',
                \'-mno-fancy-math-387',
                \'-mno-fp-ret-in-387',
                \'-mno-ieee-fp',
                \'-mno-inline-all-stringops',
                \'-mno-mmx',
                \'-mno-ms-bitfields',
                \'-mno-push-args',
                \'-mno-red-zone',
                \'-mno-rtd',
                \'-mno-soft-float',
                \'-mno-sse',
                \'-mno-sse2',
                \'-mno-sse3',
                \'-mno-svr3-shlib',
                \'-mno-tls-direct-seg-refs',
                \'-mno-win32',
                \'-mnop-fun-dllimport',
                \'-momit-leaf-frame-pointer',
                \'-mpreferred-stack-boundary=',
                \'-mpush-args',
                \'-mred-zone',
                \'-mregparm=',
                \'-mrtd',
                \'-msoft-float',
                \'-msse',
                \'-msse2',
                \'-msse3',
                \'-mstack-arg-probe',
                \'-msvr3-shlib',
                \'-mthreads',
                \'-mtls-dialect=',
                \'-mtls-direct-seg-refs',
                \'-mtune=',
                \'-mwin32',
                \'-mwindows']
    " }}}2

    let g:Mingw_tool_para_dict["g++"] = g:Mingw_tool_para_dict['gcc']
    let g:Mingw_tool_para_dict['ar'] = ['rus']
endif

" NOTE:
" è¿™æ˜¯è¡¥å…¨è·¯å¾„ç”¨çš„ï¼Œæ¯”å¦‚ -L å’Œ -I 
" è¿™ä¸ªæ²¡å¿…è¦ä»ç¯å¢ƒå˜é‡ä¸­ç”Ÿæˆï¼›
" å› ä¸ºï¼Œä½ æ—¢ç„¶ä½¿ç”¨äº†ç¯å¢ƒå˜é‡ï¼Œé‚£ä¹ˆç¼–è¯‘å™¨ã€è¿æ¥å™¨
" éƒ½èƒ½è¯»å–åˆ°è¯¥ä¿¡æ¯ï¼
if !exists("g:Mingw_usr_library_dirs") " {{{1
    let g:Mingw_usr_library_dirs = {}

    " Sarrow: 2008åä¸€æœˆ24
    let g:Mingw_usr_library_dirs['inc'] = []
    let g:Mingw_usr_library_dirs['lib'] = []

    let library_entrys = readfile(globpath(&runtimepath, 'ftplugin/make_res/3rd_part_library_dir.lst'))

    let entry_type = ""
    for entry in library_entrys
	" ä»¥#å¼€å¤´çš„æ˜¯æ³¨é‡Šï¼Œå¿½ç•¥ä¹‹
	if entry =~ '^\s*#'
	    continue
	endif
	let entry = matchstr(entry, '\zs\S.\{-0,}\ze\s*$')

	if entry =~ '^\%(lib:\|include:\)*\zs\(lib:\|include:\)\ze'
	    let entry_type = matchstr(entry, '^\%(lib:\|include:\)*\zs\(lib:\|include:\)\ze')[0:2]
	    let entry = matchstr(entry, '^\%(lib:\|include:\)*\zs.*$')
	endif
	if entry_type == "" || entry == ""
	    continue
	endif

	if entry =~ '^\$(\zs\S\+\ze)$' && expand('$'.matchstr(entry, '^\$(\zs\S\+\ze)$')) =~ ' '
	    let entry = '"'.entry.'"'
	endif
	if entry != ""
	    call add(g:Mingw_usr_library_dirs[entry_type], entry)
	endif

    endfor
    " End: 2008åä¸€æœˆ24
endif

function! Load_mingw_make_functions() " {{{1
    let functions_path = globpath(&runtimepath, 'ftplugin/make_res/functions.mak')
    let file_content = readfile(functions_path)

    let line_idx = -1
    let R_S = ""
    let R_E = ""

    " Load Region-Begin and Region-End Mark {{{2
    while line_idx < len(file_content) - 1 && (R_S == "" || R_E == "")
	let line_idx += 1
	if file_content[line_idx] =~ '^\s*#'
	    continue
	endif
	if R_S == ""
	    let R_S = matchstr(file_content[line_idx], '^Region_Start\s*=\s*\zs\S\+\ze\s*$')
	endif
	if R_E == ""
	    let R_E = matchstr(file_content[line_idx], '^Region_End\s*=\s*\zs\S\+\ze\s*$')
	endif
    endwhile	"}}}2

    " load make.functions and its describe {{{2
    let funcs = []
    while line_idx < len(file_content) - 1
	let line_idx += 1
	if file_content[line_idx] =~ '^\s*#'
	    continue
	endif
	let func_entry = matchstr(file_content[line_idx], '^\$(.\{-1,})')
	if func_entry == ""
	    continue
	endif
	let fun_describ = matchstr(file_content[line_idx], '^\$(.\{-1,})\s*#\s*\zs.\+')
	if fun_describ != ""
	    call add(funcs, {'word': func_entry, 'menu': fun_describ})
	else
	    call add(funcs, {'word': func_entry})
	endif
    endwhile	"}}}2

    return {'R_S': R_S, 'R_E': R_E, 'funcs': funcs}
endfunction

if !exists("g:Mingw_make_functions") " {{{1
    let g:Mingw_make_functions = Load_mingw_make_functions()
endif

if !exists("g:Mingw_usr_libs") " {{{1
    " NOTE:
    " gcc
    " åœ¨é“¾æ¥æ—¶å€™ï¼Œåº“çš„æŸ¥æ‰¾è·¯å¾„åˆ†ä¸ºä¸¤éƒ¨åˆ†ï¼›ä¸€éƒ¨åˆ†æ˜¯ç”±LIBRARY_PATHç¯å¢ƒå˜é‡æ§åˆ¶
    " ä¸€éƒ¨åˆ†æ˜¯ç”±ldå·¥å…·æ§åˆ¶â€”â€”é€šè¿‡ld --verboseå¯ä»¥æŸ¥è¯¢ï¼›
    " http://stackoverflow.com/questions/9922949/how-to-print-the-ldlinker-search-path
    " ç¯å¢ƒå˜é‡LIBRARY_PATHçš„ä¼˜å…ˆçº§æ›´é«˜â€”â€”è¿™æ ·ï¼Œä¾¿å¯ä»¥è¦†ç›–ç³»ç»ŸåŸæœ¬çš„åº“äº†ã€‚
    " å¦å¤–ä¸€ç§è·å–æ–¹å¼ï¼Œæ˜¯é€šè¿‡
    "   gcc -print-search-dirs
    " ï¼›å®ƒä¼šè‡ªåŠ¨å°†LIBRARY_PATHçš„é—®é¢˜ï¼Œä¹Ÿè€ƒè™‘è¿›æ¥
    "
    " å¦å¤– LIBRARY_PATH å’Œ LD_LIBRARY_PATH
    " çš„åŒºåˆ«åœ¨äºï¼Œå‰è€…ç®¡ç¼–è¯‘æ—¶ï¼Œè¿æ¥å™¨å¦‚ä½•æ‰¾åº“ï¼›åè€…ç®¡è¿è¡Œæ—¶ï¼ŒåŠ è½½å™¨å¦‚ä½•æ‰¾åº“ï¼›
    " http://stackoverflow.com/questions/4250624/ld-library-path-vs-library-path
    " å¦å¤–ï¼Œã€Šå½±å“gccçš„ç¯å¢ƒå˜é‡åˆ—è¡¨ã€‹
    " https://gcc.gnu.org/onlinedocs/gcc/Environment-Variables.html
    if exists("$LIBRARY_PATH") && len(expand("$LIBRARY_PATH")) > 0
        let g:Mingw_usr_libs = []

        let libraries = ''
        for paths in split(system("gcc -print-search-dirs"), "\n")
            if match(paths, '^libraries: =') != -1
                let libraries = strpart(paths, 12)
                break
            endif
        endfor
        for path in split(libraries, ':')
            if len(path) && path[0] == '.'
                continue
            endif
            let current_list  = split(globpath(path, "lib*.a"), "\n")
            call map(current_list, 'fnamemodify(v:val, ":t")')
            call map(current_list, '"-l".strpart(v:val, 3, len(v:val) - 5)')
            call extend(g:Mingw_usr_libs, current_list)
        endfor
        "        let library_entrys = split(expand("$LIBRARY_PATH"), ':')
        "        let library_entrys2 = split(substitute(system("ld --verbose \| grep SEARCH_DIR \| tr -s ' ;' '\n'"), 'SEARCH_DIR("\%(=\)\?\|")', '', 'g'), "\n")
        "        call extend(library_entrys, library_entrys2)
        "        for entry in library_entrys
        "            if !isdirectory(entry)
        "                continue
        "            endif
        "            let current_list  = split(globpath(entry, "lib*.a"), "\n")
        "            call map(current_list, 'fnamemodify(v:val, ":t")')
        "            call map(current_list, '"-l".strpart(v:val, 3, len(v:val) - 5)')
        "            call extend(g:Mingw_usr_libs, current_list)
        "        endfor
    else
        let g:Mingw_usr_libs = readfile(g:Mingw_usr_lib_file)
    endif
    call sort(g:Mingw_usr_libs)
endif

" è¡¥å…¨å‚æ•°
function! Make_Omni_Complete(findstart, base) " {{{1
    " vimç³»ç»Ÿä¼šåˆ†ä¸¤æ¬¡è°ƒç”¨æœ¬å‡½æ•°ã€‚
    " ç¬¬ä¸€æ¬¡è°ƒç”¨çš„æ—¶å€™ï¼Œa:findstartä¸º1ï¼›ç¬¬äºŒæ¬¡ä¸º0ï¼›
    " å½“ç¬¬ä¸€æ¬¡è°ƒç”¨çš„è¿”å›å€¼è¡¨ç¤ºè¡¥å…¨å¼€å§‹çš„åˆ—ä½ç½®ã€‚å¦‚æœä¸º-1ï¼Œåˆ™è¡¨ç¤ºå½“æ­¤è¡¥å…¨æ²¡æœ‰åŒ¹
    " é…é¡¹ç›®ï¼Œå¯¹æœ¬å‡½æ•°çš„ç¬¬äºŒæ¬¡è°ƒç”¨å°†è¢«vimå–æ¶ˆã€‚
    "
    " NOTE: æ³¨æ„ï¼Œvimä¸­ï¼Œå‡è®¾æŸä¸€è¡Œå†…å®¹ä¸ºâ€œ^|â€ï¼Œå…¶ä¸­â€œ^â€è¡¨ç¤ºè¡Œé¦–ï¼Œâ€œ|â€è¡¨
    " ç¤ºå…‰æ ‡ä½ç½®ï¼Œé‚£ä¹ˆcol('.')çš„å€¼æ˜¯1ï¼å³col('.')-1æ‰è¡¨ç¤ºçš„æ˜¯å…‰æ ‡ä¹‹å‰ä¸²çš„é•¿åº¦
    " ï¼ˆæ³¨æ„ï¼Œå¯¹äºvimæ¥è¯´ï¼Œç§°ä¸ºå­—èŠ‚æ•°æ›´ä¸ºå‡†ç¡®ï¼‰ã€‚
    " è€Œé€šè¿‡getline('.')è·å–åˆ°çš„è¡Œå†…å®¹çš„æ—¶å€™ï¼Œä¸‹æ ‡åˆ™æ˜¯ä»0å¼€å§‹çš„ã€‚

    if a:findstart
	" åº”è¯¥ä»å…‰æ ‡ä½ç½®ä¹‹å‰ä¸€ä¸ªä½ç½®å¼€å§‹æŸ¥æ‰¾
	" start_col è¡¨ç¤ºå…³é—­ä¹‹å‰çš„'å­—ç¬¦æ•°'ï¼Œæˆ–è€…è¯´'ä¸²é•¿åº¦'
        let start_col = col('.') - 1

	"  è‡³å°‘å¾—æœ‰ä¸¤ä¸ªå­—ç¬¦ä»¥ä¸Šï¼Œæ‰èƒ½åˆ¤æ–­
	if start_col <= 1
	    return -1
	endif
        let line = getline('.')[:start_col-1]
	" æœ¬æ™ºèƒ½è¡¥å…¨æä¾›ä»¥ä¸‹å‡ ç§è¡¥å…¨æ–¹å¼ï¼š
	"
	"   -	ä¹‹å‰ä¸€ä¸ªå­—ç¬¦æ˜¯ç©ºæ ¼æˆ–è€…-ï¼Œåˆ™è§¦å‘â€œå·¥å…·.å‚æ•°â€è¡¥å…¨
	"   -	ä¸ºâ€œ$(â€ï¼Œåˆ™è§¦å‘â€œå‡½æ•°â€è¡¥å…¨ï¼Œå³makeå†…å»ºå‡½æ•°
	"   -	ä¸ºâ€œ-Iâ€ï¼Œåˆ™è§¦å‘â€œinclude_dirâ€è¡¥å…¨ã€‚
	"   -	ä¸ºâ€œ-Lâ€ï¼Œåˆ™è§¦å‘â€œlib_dirâ€è¡¥å…¨ã€‚
	"   -	ä¸ºâ€œ-Dâ€ï¼Œåˆ™è§¦å‘â€œå¸¸ç”¨é¢„ç¼–è¯‘æ§åˆ¶çš„macroâ€è¡¥å…¨
	"
	if line =~ '\$(\%(\w[a-z-]*\)\=$'
	    let func_pat = matchstr(line, '\$(\%(\w[a-z-]*\)\=$')
	    let b:func_pat = escape(func_pat, '^$ \.')
	    return start_col - strlen(func_pat)
	elseif line =~ '-l\%(\i\|\.\)*$'
	    let arch_pat = matchstr(line, '-l\%(\i\|\.\)*$')
	    let b:arch_pat = arch_pat
	    return start_col - strlen(arch_pat)
	elseif line =~ '-I\s*$'
	    let inc_pat = matchstr(line, '-I\zs\s*$')
	    let b:inc_pat = 1
	    return start_col - strlen(inc_pat)
	elseif line =~ '-L\s*$'
	    let lib_pat = matchstr(line, '-L\zs\s*$')
	    let b:lib_pat = 1
	    return start_col - strlen(lib_pat)
	elseif line =~ '\s\%(-\w*\)\=$'
	    " â€œå·¥å…·.å‚æ•°â€è¡¥å…¨åˆ†ä¸ºä¸‰ç§ï¼›
	    " 1)  ^\s\+-\=@\=\w[\w+]
	    " 		å³è¡Œé¦–å°±æ˜¯å·¥å…·çš„åå­—
	    " 2) ^\s\+-\=@\=\$(\w[\w+])
	    " 		å³ä½¿ç”¨äº†å˜é‡æ¨¡å¼
	    " 		æ­¤ç§æƒ…å†µï¼Œå¯ä»¥ä½¿ç”¨normal gDæ‰¾åˆ°å®šä¹‰å¤„ï¼Œç„¶åè§£æä¹‹ï¼ˆæ­¤
	    " 		è§£æå¯èƒ½è¿˜ä¼šé‡åˆ°include other-makefileçš„é—®é¢˜
	    " 3)  ä¹‹å‰çš„ä¸€è¡Œä½¿ç”¨äº†ç»­è¡Œç¬¦â€”â€”å›æº¯è‹¥å¹²è¡Œï¼ŒçŸ¥é“é‡åˆ°æƒ…å†µ1)æˆ–è€…2)
	    let tool_name = matchstr(line, '[a-zA-Z+]\+')
	    " TODO å½“é‡åˆ° $(CXX) è¿™æ ·çš„æƒ…å†µæ—¶ï¼Œå¯ä»¥normal gDï¼Œç„¶ååˆ†æè¯¥å¤„çš„æƒ…å†µã€‚å†â€œ`.â€å›æ¥
	    if !has_key(g:Mingw_tool_para_dict, tool_name)
		return -1
	    endif
	    let b:tool_name = tool_name
	    return start_col
	else
	    return -1
	endif

	" the second time call this function
    else
	if exists('b:tool_name')
	    let tool_name = b:tool_name
	    unlet b:tool_name
	    return g:Mingw_tool_para_dict[tool_name]
	elseif exists('b:func_pat')
	    let func_pat = b:func_pat
	    unlet b:func_pat
	    let match_funcs = []
	    for entry in g:Mingw_make_functions['funcs']
		" type(entry)==type("") && entry =~ func_pat ||
	        if type(entry)==type({}) && entry['word'] =~ func_pat
		    call add(match_funcs, entry)
	        endif
	    endfor
	    return match_funcs
	elseif exists('b:arch_pat')
	    let arch_pat = b:arch_pat
	    unlet b:arch_pat
	    let matchs = []
	    for entry in g:Mingw_usr_libs
	        if entry =~ arch_pat
		    call add(matchs, entry)
	        endif
	    endfor
	    return matchs
	elseif exists('b:inc_pat')
	    unlet b:inc_pat
	    return g:Mingw_usr_library_dirs['inc']
	elseif exists('b:lib_pat')
	    unlet b:lib_pat
	    return g:Mingw_usr_library_dirs['lib']
	endif
    endif
endfun
setlocal completefunc=Make_Omni_Complete

